# はじめてのPerl6 補講

## 並行性と非同期プログラミング

### Promise

Promiseは未完了の結果をハンドルするために使われます。JavaScriptのPromiseと同じようなものです。

結果を任意に設定するには `keep` を使います。

```
my $p = Promise.new;
$p.keep("result value");
say $p.status; # Kept
say $p.result;
```

Promiseでの処理が失敗した場合は `break` で失敗理由を設定します。
`break`が設定されている状態で `result` を参照すると `die` します。

```
my $p = Promise.new;
$p.break("break reason");
say $p.status; # Broken
say $p.result; # die
```

`then` メソッドでPromiseがkeepもしくはbreakした後の実行内容をスケジュールし、新しいPromiseを返します。

```
my $p1 = Promise.new;
my $p2 = $p1.then( -> $v { say $v.result; "Second Result" } );
$p1.keep("First Result");
say $p2.result; # First Result\nSecond Result
```

```
my $p1 = Promise.new;
my $p2 = $p1.then( -> $v { say $v.result; "Second Result" } );
$p1.break("First Break Reason");
try $p2.result; # die
say $p2.cause;
```

`in` メソッドでタイマー的な使い方もできます。

```
my $p1 = Promise.in(3);
my $p2 = $p1.then( -> $v { say $v.status; "Second Result" } );
say $p2.result;
```

コードを手軽に並行に実行するには `start` メソッドが利用できます。

```
my $p = Promise.start({ my $sum = 0; for 1 .. 10 { $sum += $_ }; $sum });
say $p.result;
```

```
my $p = Promise.start({ die "Break Reson" });
try $p.result;  # die
say $p.cause;
```

また、メソッド呼び出しではなく `start` サブルーチンも用意されています。

```
my $p = start { my $sum = 0; for 1 .. 10 { $sum += $_ }; $sum };
my $result = await $p;
say $result;
```

`await`はPromiseの`result`を呼び出すのと同じですが、Promiseのリストを扱うことができます。

```
my $p1 = start { my $sum = 0; for 1 .. 10 { $sum += $_ }; $sum };
my $p2 = start { my $sum = 0; for 1 .. 10 { $sum -= $_ }; $sum };
my @results = await $p1, $p2;
say @results; # [55, -55]
```

`allof`, `anyof`メソッドで、渡されたすべてのPromise終了待ち、いずれか一つのPromiseの終了待ちが可能です。

```
my $p = Promise.allof(
  Promise.in(5).then({ say "Passed 5 sec" }),
  Promise.in(2).then({ say "Passed 2 sec" }),
);
await $p; # Passed 2sec\nPassed 5sec\n
say "All Promises done";
```

```
my $p = Promise.anyof(
  Promise.in(5).then({ say "Passed 5 sec" }),
  Promise.in(2).then({ say "Passed 2 sec" }),
);
await $p; # Passed 2sec\n
say "Any Promises done";
```


### Supplies

'Supply' は非同期データストリームを扱います。
"イベントドリブン"や"リアクティブ"と呼ばれる仕組みです。

'Supply' は`tap`で作成された複数のサブスクライバーを持つことができるメッセージストリームです。
`emit`で値を送ります。

`Supply`は`Supplier`ファクトリから作成され、`emit`された値はすべてのtapperに送信されます。

```
my $supplier = Supplier.new;
my $suuply = $supplier.Supply;

$supply.tap( -> $v { say "First Tapper: $v" });
$supply.tap( -> $v { say "Second Tapper: $v" });

for 1 .. 10 { $supplier.emit($_) }
```

また`supply`キーワードでも作成可能です。

```
my $suuply = supply {
  for 1 .. 10 { emit($_) }
}

$supply.tap( -> $v { say "First Tapper: $v" });
$supply.tap( -> $v { say "Second Tapper: $v" });
```

`interval`メソッドで定期的なイベントを発行できます。
イベント発行時の値は0からインクリメントされた整数値になります。

```
my $supply = Supply.interval(2); # 2秒ごとにイベント発行
$supply.tap( -> $v { say $v }); # 0, 1, 2, ...
sleep 10;
```

また`react`キーワードでも記述することができます。
`react`ブロックは`done()`が呼ばれるまで処理をブロックするので注意が必要です。

```
react {
  whenever Supply.interval(2) -> $v {
    say $v;
    done() if $v == 4;
  }
}
```

`Supply`では`grep`や`map`を使ってフィルタや変換が可能です。

```
my $supplier = Supplier.new;
my $supply = $supplier.Supply;

$supply.tap( -> $v { say "Original: $v" });

my $odd_supply = $supply.grep({ $_ % 2 }); # filter
$odd_supply.tap( -> $v { say "Odd: $v" });

my $even_supply = $supply.grep({ not $_ % 2 }); # filter
$even_supply.tap( -> $v { say "Even: $v" });

my $half_supply = $supply.map({ $_ / 2 }); # transform
$half_supply.tap( -> $v { say "Half: $v" });

for 1 .. 10 { $supplier.emit($_) }
```


### Channels

Golangのチャネルと同様ですが、`send`はブロックされず`recieve`はチャネルが空の場合にブロックされます。

```
my $ch = Channel.new;
$ch.send("hoge");
say $ch.receive; # hoge
```

```
my $ch = Channel.new;
say $ch.receive; # blocked!!
$ch.send("hoge");
```

チャネルに送信されたすべての値を取り出すには`list`を使います。こちらはチャネルがcloseされていない場合にブロックします。

```
my $ch = Channel.new;

for <hoge fuga piyo> -> $v { $ch.send($v) }
$ch.close;

for $ch.list -> $r { say $r }
```


Golangでいうところの"for-select loop"は"loop-if-poll"となります。

```
my $ch = Channel.new;

start {
  my $closed = $ch.closed;

  loop {
    if $ch.poll -> $item {
      say $item;
    }
    elsif $closed {
      last;
    }
  }
}

await (^5).map: -> $r {
  start {
    sleep $r;
    $ch.send($r);
  }
}

$ch.close;
```

"loop-if-poll"は"react-whenever"で書き換えることができます。

```
my $ch = Channel.new;

my $p = start {
  react {
    whenever $ch {
      say $_;
    }
  }
}

await (^5).map: -> $r { # 5つのPromiseを生成
  start {
    sleep $r;
    $ch.send($r);
  }
}

$ch.close;

await $p;
```

Supplyの`Channel`メソッドで`tap`と同様のチャネルを利用できます。

```
my $supplier = Supplier.new;
my $supply = $supplier.Supply;
my $ch = $supply.Channel;

my $p = start {
  react {
    whenever $ch {
      say $_;
    }
  }
}

await (^5).map: -> $r { # 5つのPromiseを生成
  start {
    sleep $r;
    $supplier.emit($r);
  }
}

$supplier.done;

await $p;
```

### Proc::Async

プロセスを非同期に実行します。

```
my $proc = Proc::Async.new(|<echo foo bar>);

$proc.stdout.tap(-> $v { print "Out: $v" });
$proc.stderr.tap(-> $v { print "Err $v" });

say "Start";

my $promise = $proc.start;

await $promise;

say "Done";
```

コンストラクタに`:w`を指定することで、`write`, `print`, `say`などでプロセスのSTDINにデータを渡すことが可能です。

```
my $proc = Proc::Async.new(:w, |<grep -n hoge>);

$proc.stdout.tap(-> $v { print "Out: $v" });
$proc.stderr.tap(-> $v { print "Err $v" });

say "Start";

my $promise = $proc.start;

$proc.say("foo bar buz");
$proc.say("hoge fuga piyo");
$proc.say("aaa bbb ccc");

$proc.close-stdin;

await $promise;

say "Done";
```

### Lock

Lockの`protect`メソッドで引数のコードブロックをロックして処理を保証することが可能です。

```
my $lock = Lock.new;
my $sum = 0;

await (^10).map: {
  start {
    $lock.protect({ # 複数のPromise間で処理をブロックして合計値を保証する
      my $r = rand;
      sleep $r;
      $sum++;
    });
  }
}

say $sum;
```

