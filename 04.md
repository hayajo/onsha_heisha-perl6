# はじめてのPerl6 第4回

## 補足

### サブルーチンの引数を強制する

`!`を使う。

```
sub with-mandatory-named(:$str!) {
  say "$str !!";
}

with-mandatory-named(:str("Hello World"));
with-mandatory-named(); # ERROR
with-mandatory-named("Hello World"); # ERROR
```

### 三項演算子

`??`, `!!`（Perl5では`?`, `:`）。

``
my $a = $condition ?? $true !! $false;
``

### smart match

http://doc.perl6.org/routine/~~

```
1 ~~ Int;          # type check
'hello' ~~ 'hello' # string equality
999 ~~ 999         # numeric equality
'a' ~~ /a/;        # regex match
$arg ~~ &bool-returning-function; # boolean result of invocation
'key' ~~ %hash;    # object identity([Map] exists as a key: http://doc.perl6.org/routine/ACCEPTS#class_Map)
```

### multiple dispatch

関数/メソッドのオーバーロード。`multi`キーワードで宣言する。

```
multi sub sayit(Str $s) { say "String: $s" } # 引数の型を宣言
multi sayit(Bool $b) { say "Bool: $b" } # sub は省略可能

sayit("Hello World") # "String: Hello World"
sayit(True)          # "Boo: True"
sayit(42)            # ERROR

class Klass {
  multi method meth(Mu:U:) { say 'called as class method' }
  multi method meth(Mu:D:) { say 'called as instance method' }
}

Klass.meth;
Klass.new.meth;
```

ちなみにPerl6にはクラスメソッド/インスタンスメソッドの違いはなく、クラスメソッドとして呼び出された場合は`self`はクラスオブジェクトになる。
クラスオブジェクトは未定義値扱いなので、`self.defined`でクラスメソッドとして呼ばれたかをチェック可能。

`:U`、`:D`はこちらを参照。

http://doc.perl6.org/language/faq#What_are_those_%3AD_and_%3AU_things_in_signatures%3F

`:U:`の末尾の`:`はこちらの"Methods"を参照。

https://perl6advent.wordpress.com/2013/12/21/day-21-signatures/

### precondition

引数には`where`で条件を指定することができる。

```
multi is-big(Int $n where * > 50) { "Yes !" }  # using a closure
multi is-big(Int $n where 10..50) { "Quite." } # Using smart-matching
```

### 例外

http://doc.perl6.org/language/exceptions

```
use v6;

try {
  open "myfile";
  CATCH {
    when Exception { say .message }
  }
}
```

例外クラスは`X`のパッケージ。

```
die X::AdHoc.new(payload => "Error !!!");
```

### unit

`unit`でファイルスコープを定義。

```
# in MyClass.pm6
unit class MyClass;

method mymeth {
  ...
}

...
```

### Phasers

例ではブロックを渡しているが、ブロック無しで呼び出し元と同一スコープで定義することも可能。

```
sub process($file) {
    KEEP push @success, $file;
    UNDO push @failure, $file;

    my $fh = open($file);
    # ...
}
```

### BEGIN/CHECK, INTO/END, ENTER/LEAVE

```
BEGIN { say "1. Runs during compilation." }
CHECK { say "2. Run after all compilation." }

INIT { say "3. Run just before runtime." }
END  { say "6. Run at quitting time." }

{
  ENTER { say "4. Run at enter a block" }
  LEAVE { say "5. Run at leave a block" }
}
```

### KEEP/UNDO

```
sub {
  KEEP { say "Run when exit a block successfully" }
  UNDO { say "Run when exit a block unsuccessfully" }
  #return True;
}()
```

### FIRST/NEXT/LAST

```
for 1..3 {
  FIRST { say "Run at first time of loop" }
  NEXT { say "Run at loop continuation time" }
  LAST { say "Run at loop termination time" }
  say $_;
}
```

### オペレータ

オペレータを新しく定義する。

```
sub prefix:<greet>($name) {
  say "Hello $name !";
}
greet "World";
```

### prefix, infix, postfix

```
sub prefix:<greet>($name) {
  say "Hello $name !";
}
greet "World";

sub infix:<times>(Int $n, Block $r) {
  for ^$n {
    $r();
  }
}
3 times -> { say "hoge" };

sub postfix:<!>(Int $n) {
  [*] 2..$n;
}
say 5!;

```

### circumfix, postcircumfix

```
sub circumfix:<[ ]>(Int $n) {
  $n ** $n;
}
say [3];

sub postcircumfix:<{ }>(Str $s, Int $idx) {
  $s.substr($idx, 1);
}
say "hoge"{2};
```

### MAIN

MAINサブルーチンを定義することにより、コマンドライン引数を定義することが可能。

```
sub MAIN($name) { say "Hello $name !" }
```

```
perl6 mycmd.p6
Usage:
  mycmd.p6 <name>
```

また、オーバーロードでサブコマンドも簡単に実装することが可能です。

```
subset File of Str where *.IO.e;

multi sub MAIN('add', $key, $value, Bool :$replace) { "SUBCMD: add" }
multi sub MAIN('remove', $key) { "SUBCMD: remove" }
multi sub MAIN('import', File, Str :$as) { "SUBCMD: import" }
```

```
perl6 mycmd.p6
Usage:
  mycmd.p6 [--replace] add <key> <value>
  mycmd.p6 remove <key>
  mycmd.p6 [--as=<Str>] import (File)

```

---


# Perl6でウェブプログラミング

## 環境の準備

モジュールのビルドが失敗する場合があるため、実行前に最新のrakudoをインストールします。

```
vagrant provision
vagrant ssh
```

### モジュールのインストール

* [p6-Crust](https://github.com/tokuhirom/p6-Crust)
* [Perl5 での Plack に当たる Crust をみんなで作った話](http://blog.64p.org/entry/2015/12/01/142903)

```
panda install Crust
```

### アプリケーションの実行

```
cat <<EOF >hello.psgi6
use v6;

my $app = sub (%env) {
  note %env;
  my $content = "Hello World\n";
  return
    200,
    ["Content-Type" => "text/plain"],
    [$content];
};

$app;
EOF
crustup hello.psgi6
```

```
curl 127.0.0.1:5000
```

