# はじめてのPerl6 第2回

* [Perl 6 Introductory course (PDF slides)](https://github.com/rakudo/star/raw/master/docs/2015-spw-perl6-course.pdf)

57ページから最後までを予定。

---

[メモ]

## 58ページ

* 50ページ while/untilとpointy blocksの例
* 52ページ forとpointy blocksの例

## 61ページ

Pointy Blocksでの`return`は透過的に呼び出し元のブロックを抜けてしまいます。

このためPointy Blocksでの`return`には十分注意しましょう。

```
use v6;

sub greeting() {
  my $greeting = -> $name { return "Hello $name" };

  my $msg = $greeting("World"); # ここでreturnされてしまう

  say $msg; # ここは実行されない
}

greeting();
```

## 62ページ

引数に配列やハッシュを定義しても正しく扱えます。

## 63ページ

`argument`:
サブルーチン（コード）を呼び出す際に渡すもの。0個以上のargumentを`argument list`と呼ぶ。

`parameter`:
受け取るargumentの定義。0個以上のparameterを`signature`と呼ぶ。

## 65ページ

* [LIST.pick(COUNT)](http://doc.perl6.org/type/List#routine_pick)

## 66ページ

左から右に評価され、評価済みのパラメータをデフォルト値として使用できます。

### 67ページ

名前付きパラメータを定義する場合は、パラメータの先頭に`:`をつける。

### 68ページ

```
my $global = 'GLOBAL VALUE'
say (:$global).perl # :global("GLOBAL VALUE")
```

## 70ページ

配列やハッシュを`argument list`にするには`|`オペレータで平坦化する。

```
sub concat($a, $b, $c) {
    say "$a, $b, $c";
}

my @array = <foo bar buz>;

#concat(@array); # signature mismatched
concat(|@array); # flattened as a part of argument list
```

## 71ページ

`*@arrays`パラメータは引数に渡された複数の配列を平坦化する。

平坦化したくない（複数の配列を複数の引数として扱う）場合はパラメータを`**@arrrays`(72ページ)とする。

## 75ページ

```
use v6;

sub inc($x) {
# sub inc($x is copy) {
# sub inc($x is rw) {
   ++$x;
}

my $a = 41;
inc($a);
say $a;
```

### 78ページ

* http://doc.perl6.org/language/objects#Attributes
* http://doc.perl6.org/language/variables#Twigils

[Twigilなしの場合は`!`の定義も行われるっぽい](https://github.com/perl6/specs/blob/master/S12-objects.pod#attributes)

```
has $brain;     # also declares $!brain;
```

### 79ページ

```
%!player-points.pairs.sort({ -.value })
```

↓

```
%!player-points.pairs.sort({ -$_.value })
```

### 82ページ

```
use v6;

class DaySchedule {
  has $!opening;
  has $!closing;
  has %!schedule;

  submethod BUILD(:$opening, :$closing) {
    $!opening = $opening;
    $!closing = $closing;
  }

  method add-appointment($hour, $name) {
    given $hour {
      when $_ < $!opening {
        die "too early";
      }
      when $_ >= $!closing {
        die "too late";
      }
      when %!schedule{$hour}:exists {
        die "conflict";
      }
      default {
        %!schedule{$hour} = $name;
      }
    }
  }

  method print-appointment() {
    %!schedule.pairs.sort({ Int(.key) }).map({ say .fmt("%02d: %s") });
  }
}

my $sched = DaySchedule.new(opening => 9, closing => 17);
$sched.add-appointment(10, 'Fred'); # Works fine
# $sched.add-appointment(8, 'Brad');  # Dies: too early
# $sched.add-appointment(17, 'Anna'); # Dies: too late
$sched.add-appointment(9, 'Lena');  # Works fine
# $sched.add-appointment(10, 'Adam'); # Dies: conflict

$sched.print-appointment();
```
